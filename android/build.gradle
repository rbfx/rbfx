apply plugin: 'com.android.application'

// Relative path to engine source code directory. Modify as required.
def projectSourceDir = '..'
// Relative path to project source code directory. Modify as required.
def rbfxSourceDir = '..'
// Minimum required CMake version. This is same as in ../CMakeLists.txt.
def cmakeVersion = '3.14.0.0+'

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion '29.0.2'
    defaultConfig {
        applicationId 'io.urho3d'
        versionCode 1
        versionName '1.0.0'
        minSdkVersion 17
        targetSdkVersion 28
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64'
        }
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_static', '-DANDROID_PLATFORM=android-21', '-DANDROID=1',
                          '-DANDROID_ARM_MODE=arm', '-DBUILD_SHARED_LIBS=ON', '-DURHO3D_SAMPLES=ON',
                          '-DURHO3D_PLAYER=OFF'
            }
        }
    }
    externalNativeBuild {
        cmake {
            version "${cmakeVersion}"
            path "${projectSourceDir}/CMakeLists.txt"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt')

        }
    }
    sourceSets.main {
        manifest.srcFile 'AndroidManifest.xml'
        java.srcDirs = ['src', "${rbfxSourceDir}/Source/ThirdParty/SDL/android-project/app/src/main/java"]
        res.srcDirs = ['res']
        assets.srcDirs = ['assets', "${rbfxSourceDir}/bin"]
    }
    dexOptions {
        javaMaxHeapSize "4g"
        // If you get "Java heap error" you may need to add "org.gradle.jvmargs=-Xms128m -Xmx4096m" to gradle.properties
        // in gradle service directory path (find it in Settings>Build>Gradle).
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    ndkVersion '20.0.5594570'
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}
