apply plugin: 'com.android.application'

// Relative path to engine source code directory. Modify as required.
def projectSourceDir = '..'
// Relative path to project source code directory. Modify as required.
def rbfxSourceDir = '..'
// Minimum required CMake version. This is same as in ../CMakeLists.txt.
def cmakeVersion = '3.22.0+'

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.0'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

android {
    namespace 'io.urho3d'
    compileSdkVersion 35
    defaultConfig {
        applicationId 'io.urho3d'
        versionCode 1
        versionName '1.0.0'
        minSdkVersion 24
        targetSdkVersion 35
        ndk {
            // Use environment variable to build only specific architecture if set
            def targetArch = System.getenv('ci_arch')
            if (targetArch) {
                abiFilters targetArch
            } else {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64'
            }
        }
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_static', '-DANDROID_PLATFORM=android-21', '-DANDROID=1',
                          '-DBUILD_SHARED_LIBS=ON', '-DURHO3D_SAMPLES=ON', '-DURHO3D_PLAYER=OFF',
                          '-DCMAKE_POLICY_VERSION_MINIMUM=3.5'

                def cmakePrefixPath = System.getenv('CMAKE_PREFIX_PATH')
                if (cmakePrefixPath) {
                    arguments "-DCMAKE_PREFIX_PATH=${cmakePrefixPath}"
                }
            }
        }
    }
    externalNativeBuild {
        cmake {
            version "${cmakeVersion}"
            path "${projectSourceDir}/CMakeLists.txt"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')

        }
    }
    sourceSets.main {
        manifest.srcFile 'AndroidManifest.xml'
        java.srcDirs = ['src', "${rbfxSourceDir}/Source/ThirdParty/SDL/android-project/app/src/main/java"]
        res.srcDirs = ['res']
        assets.srcDirs = ['assets', "${rbfxSourceDir}/bin"]
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}
